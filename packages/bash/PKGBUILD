# Arch-OS X Maintainer: Sean Yeh <snoblo at archlinux dot us>
# $Id: PKGBUILD 82719 2010-06-16 04:44:35Z allan $
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=bash
_patchlevel=007 #prepare for some patches
pkgver=4.1.$_patchlevel
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=('macx86')
license=('GPL')
url="http://www.gnu.org/software/bash/bash.html"
groups=('base')
backup=(opt/arch/etc/profile.bash opt/arch/etc/skel/.bashrc opt/arch/etc/skel/.bash_profile)
depends=('readline>=6.1')
makedepends=(gzip)
provides=('sh')
install=bash.install
source=(http://ftp.gnu.org/gnu/bash/bash-4.1.tar.gz
        profile.bash
        bashrc
        enable-system-config-files.patch
        system.bashrc)
if [ $_patchlevel -gt 000 ]; then
    for (( p=1; p<=$_patchlevel; p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-4.1-patches/bash41-$(printf "%03d" $p))
    done
fi

build() {
  cd ${srcdir}/${pkgname}-4.1
  for p in ../bash41-*; do
    [ -e "$p" ] || continue
    msg "applying patch ${p}"
    patch -Np0 -i ${p} || return 1
  done

  patch -Np0 -i $srcdir/enable-system-config-files.patch || return 1

  ./configure --prefix=/opt/arch --with-curses --enable-readline \
    --without-bash-malloc --with-installed-readline \
    --bindir=/opt/arch/bin --mandir=/opt/arch/share/man --infodir=/opt/arch/share/info
  make || return 1
  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/profile.bash ${pkgdir}/opt/arch/etc/profile.bash || return 1
  install -D -m644 ${srcdir}/system.bashrc ${pkgdir}/opt/arch/etc/bash.bashrc || return 1

  # for now, bash is our default /bin/sh
  cd ${pkgdir}/opt/arch/bin
  ln -s bash sh

  mkdir -p ${pkgdir}/opt/arch/etc/skel/
  install -D -m644 ${srcdir}/bashrc ${pkgdir}/opt/arch/etc/skel/.bashrc
  echo ". \$HOME/.bashrc" >${pkgdir}/opt/arch/etc/skel/.bash_profile
}

md5sums=('9800d8724815fd84994d9be65ab5e7b8'
         '13c5ce84d88252e2c6abe661267d30ad'
         '3d2837e84645eac9bde399b58014bafa'
         'a84f77591434ff15e147cd41e0674a7e'
         '05ebcf1878494549a3d4e64996100869'
         '582dea5671b557f783e18629c2f77b68'
         '118d465095d4a4706eb1d34696a2666a'
         '120f7cf039a40d35fe375e59d6f17adc'
         '336ee037fc2cc1e2350b05097fbdc87c'
         '9471e666797f0b03eb2175ed752a9550'
         'fb80ccd58cb1e34940f3adf4ce6e4a1e'
         '192a8b161d419a1d0d211169f1d1046e')

