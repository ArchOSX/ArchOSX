# Arch-OS X Maintainer: Sean Yeh <snoblo at archlinux dot us>
# $Id: PKGBUILD 82447 2010-06-11 08:40:21Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz
pkgver=4.999.9beta
_snapshot=143-g3e49
pkgrel=5
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('macx86')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('sh')
provides=('lzma' 'lzma-utils' 'xz-utils')
replaces=('lzma' 'lzma-utils' 'xz-utils')
conflicts=('lzma' 'lzma-utils' 'xz-utils')
options=('!libtool')
source=("http://tukaani.org/${pkgname}/${pkgname}-${pkgver}-${_snapshot}.tar.gz")
md5sums=('9e5a42d2b5277d8d71405d065120bd4e')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

build() {
	cd ${srcdir}/${pkgname}-$pkgver-${_snapshot}

	./configure --prefix=/opt/arch \
		--enable-dynamic \
		--disable-rpath 
#		--enable-werror
	sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
	sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool
	make
}

package() {
	cd ${srcdir}/${pkgname}-$pkgver-${_snapshot}
	make DESTDIR=${pkgdir} install
	install -d -m755 ${pkgdir}/opt/arch/share/licenses/xz/
	ln -sf /opt/arch/share/doc/xz/COPYING ${pkgdir}/opt/arch/share/licenses/xz/
	ln -sf /opt/arch/share/licenses/common/GPL2/license.txt ${pkgdir}/opt/arch/share/doc/xz/COPYING.GPLv2
}
