# Arch-OS X Maintainer: Sean Yeh <snoblo at archlinux dot us>
# $Id: PKGBUILD 84561 2010-06-30 23:36:38Z dgriffiths $
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=curl
pkgver=7.21.0
pkgrel=1
pkgdesc="An URL retrival utility and library"
arch=('macx86')
url="http://curl.haxx.se"
license=('MIT')
depends=('zlib' 'openssl' 'bash' 'ca-certificates')
options=('!libtool')
source=("http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2")
md5sums=('e1a2a773e93a39f3c04cab92c55bf197')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure \
	    --with-random=/dev/urandom \
	    --prefix=/opt/arch \
	    --mandir=/opt/arch/share/man \
	    --disable-dependency-tracking \
	    --enable-ipv6 \
	    --disable-ldaps \
	    --disable-ldap \
	    --enable-manual \
	    --enable-versioned-symbols \
	    --with-ca-path=/opt/arch/etc/ssl/certs \
	    --without-libidn || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install || return 1
	install -Dm644 COPYING \
		${pkgdir}/opt/arch/share/licenses/${pkgname}/COPYING || return 1
	install -Dm644 docs/libcurl/libcurl.m4 \
		${pkgdir}/opt/arch/share/aclocal/libcurl.m4 || return 1
}
