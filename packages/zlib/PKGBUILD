# Arch-OS X Maintainer: Sean Yeh <snoblo at archlinux dot us>
# $Id: PKGBUILD 80715 2010-05-20 20:58:22Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=zlib
pkgver=1.2.5
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('macx86')
license=('custom')
url="http://www.zlib.net/"
depends=()
options=('!makeflags')
source=("http://zlib.net/zlib-${pkgver}.tar.gz"
        'zlib-1.2.5-lfs-decls.patch')
md5sums=('c735eab2d659a96e5a594c9e8541ad63'
         '4cb279ea3beab621f3526bf7b7ab99e5')

build() {
	cd ${srcdir}/zlib-$pkgver
	# see http://bugs.archlinux.org/task/19280
	patch -p1 -i ${srcdir}/zlib-1.2.5-lfs-decls.patch || return 1

	#patch from macports?
#	patch -p1 -i ${startdir}/patch-Makefile.in.diff || return 1

	export CFLAGS="${CFLAGS/-O2/-O3} -DUNALIGNED_OK"
	CFLAGS="$CFLAGS -fPIC -dynamiclib -flat_namespace -undefined suppress"
	./configure --prefix=/opt/arch --shared --libdir=/opt/arch/lib --includedir=/opt/arch/include
	make || return 1

	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

package() {
	cd ${srcdir}/zlib-$pkgver
	
	make install DESTDIR=${pkgdir} || return 1
	chmod 644 ${pkgdir}/opt/arch/lib/libz.a

	mkdir -p ${pkgdir}/opt/arch/share/licenses/zlib
	install -m644 LICENSE ${pkgdir}/opt/arch/share/licenses/zlib/LICENSE
}
