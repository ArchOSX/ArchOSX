# Arch-OS X Maintainer: Sean Yeh <snoblo at archlinux dot us>
# $Id: PKGBUILD 83737 2010-06-22 18:32:52Z andrea $
# Maintainer:
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=libldap
pkgver=2.4.22
pkgrel=1
pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries"
arch=('macx86')
license=('custom')
url="http://www.openldap.org/"
backup=(opt/arch/etc/openldap/ldap.conf)
depends=('libsasl' 'openssl')
#makedepends=('tcp_wrappers')
options=('!libtool')
source=("ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz"
        'ntlm.patch')
md5sums=('ef01b52255ce8e3fbf8aa34f6fe7598b'
         'dfa9eb6f2fd9d0a3dab0e1860923489e')

build() {
  cd ${srcdir}/openldap-${pkgver}

  patch -Np1 -i ${srcdir}/ntlm.patch

  ./configure --prefix=/opt/arch \
              --libexecdir=/opt/arch/sbin \
              --sysconfdir=/opt/arch/etc \
	      --mandir=/opt/arch/share/man \
              --localstatedir=/opt/arch/var/lib/openldap \
              --enable-crypt --enable-dynamic \
              --with-threads --enable-wrappers \
	      --enable-spasswd --with-cyrus-sasl \
	      --disable-bdb --disable-hdb

  cd include
  make

  cd ../libraries
  make depend
  make

  cd ../doc/man/man3
  make
  
  cd ../man5
  make
}

package() {
  cd ${srcdir}/openldap-${pkgver}

  cd include
  make DESTDIR=${pkgdir} install

  cd ../libraries
  make DESTDIR=${pkgdir} install

  cd ../doc/man/man3
  make DESTDIR=${pkgdir} install

  cd ../man5
  install -Dm644 ldap.conf.5.tmp \
    ${pkgdir}/opt/arch/share/man/man5/ldap.conf.5
  
  # get rid of duplicate default conf files
  rm ${pkgdir}/opt/arch/etc/openldap/*.default

  ln -sf liblber.so ${pkgdir}/opt/arch/lib/liblber.so.2
  ln -sf libldap.so ${pkgdir}/opt/arch/lib/libldap.so.2

  install -Dm644 ${srcdir}/openldap-${pkgver}/LICENSE \
    ${pkgdir}/opt/arch/share/licenses/$pkgname/LICENSE
}
